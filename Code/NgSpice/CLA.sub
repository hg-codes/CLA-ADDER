.include AND.sub
.include 3_AND.sub
.include 4_AND.sub
.include 5_AND.sub
.include OR.sub
.include 3_OR.sub
.include 4_OR.sub
.include 5_OR.sub
.include XOR.sub
.include Dff.sub

.subckt CLA Cin A0 A1 A2 A3 B0 B1 B2 B3 S0 S1 S2 S3 Cout Vdd Gnd
    
    * Carry look ahead adder
    * clk : clock
    * A0, A1, A2, A3: input bits of A
    * B0, B1, B2, B3: input bits of B
    * Cin: input carry
    * S0, S1, S2, S3: output bits of sum
    * Cout: output carry
    * vdd, gnd: power and ground nodes

    * Carry Generation
    * gi=AiBi
    xand0 A0 B0 G0 vdd gnd AND
    xand1 A1 B1 G1 vdd gnd AND
    xand2 A2 B2 G2 vdd gnd AND
    xand3 A3 B3 G3 vdd gnd AND

    * Carry Propagation
    * pi=Ai^Bi
    xxor0 A0 B0 P0 vdd gnd XOR
    xxor1 A1 B1 P1 vdd gnd XOR
    xxor2 A2 B2 P2 vdd gnd XOR
    xxor3 A3 B3 P3 vdd gnd XOR

    * Carry Calculation Using Carry Generation and Propagation
    
    * c0=g0+p0Cin
    xand4 P0 Cin P0Cin vdd gnd AND
    xor0 G0 P0Cin C0 vdd gnd OR

    * c1=g1+p1g0+p1p0Cin
    x3and0 P1 P0 Cin P1P0Cin vdd gnd 3_AND
    xand5 P1 G0 P1G0 vdd gnd AND
    x3or0 P1G0 P1P0Cin G1 C1 vdd gnd 3_OR

    * c2=g2+p2g1+p2p1g0+p2p1p0Cin
    x4and0 P2 P1 P0 Cin P2P1P0Cin vdd gnd 4_AND
    x3and1 P2 P1 G0 P2P1G0 vdd gnd 3_AND
    xand6 P2 G1 P2G1 vdd gnd AND
    x4or0 P2G1 P2P1P0Cin P2P1G0 G2 C2 vdd gnd 4_OR

    * c3 or cout=g3+p3g2+p3p2g1+p3p2p1g0+p3p2p1p0Cin
    x5and0 P3 P2 P1 P0 Cin P3P2P1P0Cin vdd gnd 5_AND
    x4and1 P3 P2 P1 G0 P3P2P1G0 vdd gnd 4_AND
    x3and2 P3 P2 G1 P3P2G1 vdd gnd 3_AND
    xand7 P3 G2 P3G2 vdd gnd AND
    x5or0 P3G2 P3P2P1P0Cin P3P2P1G0 P3P2G1 G3 Cout vdd gnd 5_OR

    * Sum Calculation
    * si=pi^Ci
    xxor4 P0 Cin S0 vdd gnd XOR 
    xxor5 P1 C0 S1 vdd gnd XOR
    xxor6 P2 C1 S2 vdd gnd XOR
    xxor7 P3 C2 S3 vdd gnd XOR

.ends CLA